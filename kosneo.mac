	.enabl	LC, AMA
	.list	MEB
	.asect
	.=1000

XSize		= 256.		; pixels
YSize		= 256.		; pixels
ScreenBuffer	= 40000		; address

TablSize	= 64.
Angle		= 16.			; Cos offset
Variable	= Accums + 6

Start:
	mov	#PaletteConfig, @#177530	; create palette
	nop					; believe it or not, NOP next to a system call is necessary
	mov	PaletteId, AreaPalette
	mov	#AreaConfig, @#177530		; create screen buffer (so called "area")
	nop
	mov	AreaConfig+2, @#AreaId		; put a screen buffer id to the window description
	mov	#ViewConfig, @#177530		; create window (so called "view")
	nop

	jmp	Speed

PaletteConfig:	.word	17			; command "create palette"
PaletteId:	.byte	1			; 4 colors
		.byte	0			; palette #
		; palette words G5 G4 G3│R4 R3 R2│B4 B3│ - │G2 G1 G0│R1 R0│B2 B1 B0
PaletteBuf:
		.byte	0,0,0,0,0,0,0,0,0,0,0,0 ; Двенадцать незначащих нулей
		.byte	0, 3, 340, 34 		; Старшие части RGB
		.byte	0,0,0,0,0,0,0,0,0,0,0,0 ; Двенадцать незначащих нулей
		.byte	0, 7, 340, 30 		; Младшие части RGB

; screen buffer (so called "area") description
AreaConfig:	.word	46			; command "create area"
		.word	ScreenBuffer		; address of screen buffer or area id
		.word	YSize			; rows count
		.word	XSize/4			; bytes count in row
AreaPalette:	.byte	1			; 4 colors
		.byte	0			; palette 0
; window (so called "view") description
ViewConfig:	.word	4			; command "create view"
ViewId:		.word	0			; view id
AreaId:		.word	0			; area id
		.word	0			; vertical content offset
BXBY:		.word	0			; horisontal content offset
		.word	0			; top position (row)
		.word	256.			; bottom position (row)
		.word	0			; left position (column)
		.word	16.			; right position (column)
		.byte	1,2			; scale x,y (0 = *4, 1 = *2, 2 = *1)


Speed:	br	Begin				; Y
	.word	36543, 1321			; timer2, timer1
Accums:

Begin:
	; Clone sine table
	mov	#Tabl+32., R1			; pointer to sine table
	mov	#32., R2
10$:	movb	Tabl-1(R2), (R1)+
	sob	R2, 10$

;	; clear screen
;20$:	clr	(R1)+
;	tst	R1
;	bpl	20$

Loop:	mov	#Accums, R3
	clr	(R3)+				; clear accums
	clr	(R3)+
	tst	(R3)+
	mov	#100, R0			; mask

Direct:	incb	(R3)				; default Y
	bne	10$
	xor	R0, Direct			; DEC opcode: 5313, INC opcode: 5213
10$:	mov	(R3)+, (R3)			; set Y
	mov	(R3)+, (R3)			; set timer2
	mov	#40200, R0			; screen address

; render twister to screen
Render:
	mov	#Speed, R4
	mov	#3, R3
10$:	add	(R4)+, 4(R4)			; add speed to accum
	adc	14(R4)				; increment variable
	sob	R3, 10$

	mov	#Variable+6, R3			; pointer to variable array

	mov	(R3), R4			; get timer 1
	call	Sin
	swab	R5
	asl	R5
	mov	R5, Speed			; Y speed

	mov	-(R3), R4			; get timer 2
	call	Sin
	add	#32., R5
	asr	R5
	asr	R5
	mov	R5, R2				; horisontal shift

	mov	-(R3), R4			; get Y

	mov	R0, -(SP)			; set maximum
	mov	R0, -(SP)
	add	#77, (SP)			; set minimum
	mov	#4, R3
30$:	call	Sin				; R5=Sin(R4)
	mov	R5, R1
	asr	R1				; left coordinate
	add	#Angle, R4			; 90 degrees offset
	call	Sin				; R5=Sin(R4)
	asr	R5				; right coordinate
	sub	R1, R5				; length
	ble	70$
	bic	#77, R0				; new screen line
	add	R2, R0				; horisontal shift
	add	R1, R0
	cmp	R0, (SP)			; minimum
	bgt	40$
	mov	R0, (SP)			; set new minimum
40$:	movb	Color-1(R3), R1			; read color
50$:	movb	R1, (R0)+
	sob	R5, 50$
	cmp	R0, 2(SP)			; maximum
	blos	60$
	mov	R0, 2(SP)			; set new maximum
60$:	bicb	#340, -(R0)			; draw border
70$:	sob	R3, 30$
	mov	(SP)+, R5			; get minimum
	movb	R3, -(R5)			; erase left pixels
	movb	R3, -(R5)
	movb	R3, -(R5)
	movb	R3, -(R5)
	mov	(SP)+, R5			; get maxumum
	movb	R3, (R5)+			; erase right pixels
	movb	R3, (R5)+
	mov	R3, (R5)

	add	#300, R0			; next screen line
	bpl	Render				; end of screen?

	jmp	Loop				; next frame


; Sinus
; input: R4 - angle
; outpu: R5 - sinus
Sin:	mov	R4, R5
	bic	#177700, R5
	movb	Tabl(R5), R5
	return

Color:	.byte	231,167,252,273
Tabl:	.byte	63.,62.,62.,61.,60.,58.,56.,54.,52.,50.,48.,45.,42.,39.,37.,33.
	.byte	31.,28.,25.,22.,19.,17.,14.,12.,10.,8.,6.,4.,3.,2.,2.,1.

	.end	Start
